plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolVersion

    defaultConfig {
        applicationId "com.makeus.milliewillie"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        testOptions {
            unitTests {
                returnDefaultValues = true
                includeAndroidResources = true
            }

            animationsDisabled = true
        }
    }

    signingConfigs {
        debug {
            storeFile file(SIGNED_STORE_FILE)
            storePassword SIGNED_STORE_PASSWORD
            keyAlias SIGNED_KEY_ALIAS
            keyPassword SIGNED_KEY_PASSWORD
        }
    }


    buildTypes {
        debug{
            minifyEnabled false
            shrinkResources false

            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "makeUs"
    productFlavors {
        dev {
            dimension "makeUs"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            resValue "string", "app_name", rootProject.appNameDev
            buildConfigField "String", "API_URL", rootProject.apiUrlDev
            manifestPlaceholders = rootProject.manifestPlaceholdersDev
            buildConfigField "String", "KAKAO_APP_KEY", rootProject.kakaoAppKeyDev
            buildConfigField "String", "KAKAO_OAUTH_APP_KEY", rootProject.kakaoOauthAppKeyDev

            copy {
                from 'src/dev/'
                include '*.json'
                into '.'
            }
        }



        stage {
            dimension "makeUs"
            applicationIdSuffix ".stage"
            versionNameSuffix "-stage"
            resValue "string", "app_name", rootProject.appNameStage
            buildConfigField "String", "API_URL", rootProject.apiUrlStage
            manifestPlaceholders = rootProject.manifestPlaceholdersStage
            buildConfigField "String", "KAKAO_APP_KEY", rootProject.kakaoAppKeyStage
            buildConfigField "String", "KAKAO_OAUTH_APP_KEY", rootProject.kakaoOauthAppKeyStage

            copy {
                from 'src/stage/'
                include '*.json'
                into '.'
            }
        }

        real {
            dimension "makeUs"
            resValue "string", "app_name", rootProject.appName
            buildConfigField "String", "API_URL", rootProject.apiUrl
            manifestPlaceholders = rootProject.manifestPlaceholders
            buildConfigField "String", "KAKAO_APP_KEY", rootProject.kakaoAppKey
            buildConfigField "String", "KAKAO_OAUTH_APP_KEY", rootProject.kakaoOauthAppKey

            copy {
                from 'src/real/'
                include '*.json'
                into '.'
            }
        }


    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && (variant.getFlavors().get(0).name == 'dev' || variant.getFlavors().get(0).name == 'stage')
                || (variant.buildType.name == 'debug' && variant.getFlavors().get(0).name == 'real')) {
            variant.setIgnore(true)
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = rootProject.jvmTargetVersion
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {


    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':base')

    //calendar picker
    implementation "com.andrewjapar.rangedatepicker:rangedatepicker:$calendarpickerVersion"

    //calendar
    implementation "com.github.prolificinteractive:material-calendarview:${calendarVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"

    //lottie
    implementation "com.airbnb.android:lottie:$lottieVersion"
    // androidx
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorVersion"

    //recycler view
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // RxJava2
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebaseBom")
    implementation "com.google.firebase:firebase-messaging:$firebaseMessageVersion"
    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation "com.google.firebase:firebase-auth:$firebaseAuthVersion"
    implementation "com.google.android.gms:play-services-auth:$firebasePlayServiceAuthVersion"

    // OKHttp3 + Retrofit2
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // Koin - Kotlin Injection
    implementation "org.koin:koin-android:$koinAndroidVersion"
    implementation "org.koin:koin-android-viewmodel:$koinAndroidVersion"

    // Gson
    implementation "com.google.code.gson:gson:$gsonVersion"

    // SecurePreference
    implementation "com.scottyab:secure-preferences-lib:$securePreferenceVersion"

    //permission
    implementation "gun0912.ted:tedpermission-rx2:$tedPermission"

    //Android Version Info Api Caller
    implementation "org.jsoup:jsoup:$jsoupVersion"

    //stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    //kakao
    implementation "com.kakao.sdk:v2-user-rx:$kakaoVersion" // 카카오 로그인

    testImplementation "junit:junit:$junitVersion"

    testImplementation "org.koin:koin-test:$koinAndroidVersion"

    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core:$testCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitTestVersion"
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'